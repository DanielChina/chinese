import java.text.SimpleDateFormat

apply plugin: 'java'
apply plugin: 'me.champeau.gradle.jmh'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply from: '../config/quality-java.gradle'

group = rootProject.ext.groupName
version = rootProject.ext.releaseVersionName

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "me.champeau.gradle:jmh-gradle-plugin:0.3.1"
        classpath 'org.apache.ant:ant:1.9.7'
    }

}

sourceCompatibility = 1.6
targetCompatibility = 1.6

jmh {
    jmhVersion = '1.3.3'
    include = ['*.PinyinDictBenchmark.*']
    operationsPerInvocation = 10
    benchmarkMode = ['thrpt']
    verbosity = 'NORMAL'
    fork = 2
    timeUnit = 'us'
    duplicateClassesStrategy = 'warn'
}

configurations {
    jmh
}

sourceSets {
    jmh
}

project.sourceSets.jmh {
    compileClasspath += project.configurations.jmh + project.sourceSets.main.output
    runtimeClasspath += project.configurations.jmh + project.sourceSets.main.output
}

task jmhJarFixed(type: Jar, dependsOn: jmhClasses) {
    doFirst {
        from (project.configurations.jmh.collect {it.isDirectory() ? it : project.zipTree(it)}) {
            exclude '**/META-INF/services/**'
            exclude '**/META-INF/*.SF'
            exclude '**/META-INF/*.DSA'
            exclude '**/META-INF/*.RSA'
        }
        from project.sourceSets.jmh.output
        from project.sourceSets.main.output
        duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    }

    manifest {
        attributes 'Main-Class':'org.openjdk.jmh.Main'
    }

    classifier = 'jmh'
}

task jmhFixed(type: JavaExec, dependsOn: jmhJarFixed) {
    main = 'org.openjdk.jmh.Main'
    classpath = project.files(project.jmhJar.archivePath) + project.sourceSets.main.runtimeClasspath
}


dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.assertj:assertj-core:1.7.1'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile 'com.belerweb:pinyin4j:2.5.0'

    compile 'org.ahocorasick:ahocorasick:0.3.0'

    jmh fileTree(dir: 'jmh-libs', include: ['*.jar'])
    jmh 'org.apache.commons:commons-lang3:3.0'
    jmh 'com.belerweb:pinyin4j:2.5.0'
    jmh 'org.openjdk.jmh:jmh-core:1.3.3'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.3.3'
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            if (plugins.hasPlugin('war')) {
                from components.web
            } else {
                from components.java
            }

            artifact sourcesJar
            artifact javadocJar

            artifactId 'tinypinyin'
        }
    }
}

bintray {
    user = bintray_user
    key = bintray_api_key

    publications = ['mavenJava']

    dryRun = false
    publish = true
    pkg {
        repo = 'maven'
        name = 'tinypinyin'
        desc = '适用于Java和Android的快速、低内存占用的汉字转拼音库。'
        websiteUrl = 'https://github.com/promeG/TinyPinyin'
        issueTrackerUrl = 'https://github.com/promeG/TinyPinyin/issues'
        vcsUrl = 'https://github.com/promeG/TinyPinyin.git'
        licenses = ['Apache-2.0']
        labels = ['pinyin', 'java', 'android']
        publicDownloadNumbers = true
        //Optional version descriptor
        version {
            name = rootProject.ext.releaseVersionName //Bintray logical version name
            released = new SimpleDateFormat('yyyy-MM-dd\'T\'HH:mm:ss.SSSZZ').format(new Date())
            vcsTag = '1.0.0'
            mavenCentralSync {
                sync = false
                //Optional (true by default). Determines whether to sync the version to Maven Central.
                user = 'userToken' //OSS user token
                password = 'paasword' //OSS user password
                close = '1'
                //Optional property. By default the staging repository is closed and artifacts are released to Maven Central. You can optionally turn this behaviour off (by puting 0 as value) and release the version manually.
            }
        }
    }
}